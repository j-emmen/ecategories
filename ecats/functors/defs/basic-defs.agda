
{-# OPTIONS --without-K #-}

module ecats.functors.defs.basic-defs where

open import ecats.basic-defs.ecat-def&not
open import ecats.basic-defs.isomorphism
open import ecats.functors.defs.efunctor-d&n
open import ecats.functors.defs.natural-transformation



-- Adjunctions

record adjunction {â„‚ ğ”» : ecategory} (L : efunctor â„‚ ğ”») (R : efunctor ğ”» â„‚) : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module L = efunctor L
    module R = efunctor R
  field
    Î· : natural-transformation IdF (R â—‹ L)
    Îµ : natural-transformation (L â—‹ R) IdF
  open natural-transformation Îµ renaming (fnc to Îµ-f; nat to Îµ-n)
  open natural-transformation Î· renaming (fnc to Î·-f; nat to Î·-n)
  field
    tridâ‚ : {X : â„‚.Obj} â†’ Îµ-f {L.â‚’ X} ğ”».âˆ˜ L.â‚ Î·-f ğ”».~ ğ”».idar (L.â‚’ X)
    tridâ‚‚ : {Y : ğ”».Obj} â†’ R.â‚ Îµ-f â„‚.âˆ˜ Î·-f {R.â‚’ Y} â„‚.~ â„‚.idar (R.â‚’ Y)

infix 3 _âŠ£_

_âŠ£_ : {â„‚ ğ”» : ecategory} â†’ (L : efunctor â„‚ ğ”») â†’ (R : efunctor ğ”» â„‚) â†’ Setâ‚
L âŠ£ R = adjunction L R


-- Equivalences

record is-equivalence-pair {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») (G : efunctor ğ”» â„‚) : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor F
    module G = efunctor G
  field
    Î¹1 : natural-iso (F â—‹ G) IdF
    Î¹2 : natural-iso (G â—‹ F) IdF
  module Î¹1 = natural-iso Î¹1
  module Î¹2 = natural-iso Î¹2

record is-adj-equivalence-pair {â„‚ ğ”» : ecategory}(F : efunctor â„‚ ğ”»)(G : efunctor ğ”» â„‚)
                               --(eqvp : is-equivalence-pair F G)
                               : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor-aux F
    module G = efunctor-aux G
  --open is-equivalence-pair eqvp
  field
    Î¹1 : natural-iso (F â—‹ G) IdF
    Î¹2 : natural-iso (G â—‹ F) IdF
  module Î¹1 = natural-iso Î¹1
  module Î¹2 = natural-iso Î¹2
  -- in this case the triangular identities say that
  -- F Î¹2 ~ Î¹1 F and G Î¹1 ~ Î¹2 G, respectively.
  field
    tridâ‚ : {X : â„‚.Obj} â†’ Î¹1.fnc {F.â‚’ X} ğ”».âˆ˜ F.â‚ Î¹2.fncâ»Â¹ ğ”».~ ğ”».idar (F.â‚’ X)
    tridâ‚‚ : {Y : ğ”».Obj} â†’ G.â‚ Î¹1.fnc â„‚.âˆ˜ Î¹2.fncâ»Â¹ {G.â‚’ Y} â„‚.~ â„‚.idar (G.â‚’ Y)
  -- triangle identities for the inverses  
  tridâ»Â¹â‚ : {X : â„‚.Obj} â†’ F.â‚ Î¹2.fnc ğ”».âˆ˜ Î¹1.fncâ»Â¹ {F.â‚’ X} ğ”».~ ğ”».idar (F.â‚’ X)
  tridâ»Â¹â‚ {X} = ~proof F.â‚ Î¹2.fnc ğ”».âˆ˜ Î¹1.fncâ»Â¹ {F.â‚’ X}            ~[ ridggË¢ r tridâ‚ âŠ™ assË¢ ] /
                       F.â‚ Î¹2.fnc ğ”».âˆ˜ Î¹1.fncâ»Â¹ {F.â‚’ X} ğ”».âˆ˜ Î¹1.fnc {F.â‚’ X} ğ”».âˆ˜ F.â‚ Î¹2.fncâ»Â¹
                                                                  ~[ âˆ˜e (ass âŠ™ lidgg r Î¹1.iddom) r ] /
                       F.â‚ Î¹2.fnc ğ”».âˆ˜ F.â‚ Î¹2.fncâ»Â¹                ~[ F.âˆ˜ax Î¹2.idcod âŠ™ F.id ]âˆ
                       ğ”».idar (F.â‚’ X) âˆ
              where open ecategory-aux-only ğ”»
  tridâ»Â¹â‚‚ : {Y : ğ”».Obj} â†’ Î¹2.fnc {G.â‚’ Y} â„‚.âˆ˜ G.â‚ Î¹1.fncâ»Â¹ â„‚.~ â„‚.idar (G.â‚’ Y)
  tridâ»Â¹â‚‚ {Y} = ~proof Î¹2.fnc {G.â‚’ Y} â„‚.âˆ˜ G.â‚ Î¹1.fncâ»Â¹         ~[ ridggË¢ r tridâ‚‚ âŠ™ assË¢ ] /
                       Î¹2.fnc {G.â‚’ Y} â„‚.âˆ˜ G.â‚ Î¹1.fncâ»Â¹ â„‚.âˆ˜ G.â‚ Î¹1.fnc â„‚.âˆ˜ Î¹2.fncâ»Â¹ {G.â‚’ Y}
                                      ~[ âˆ˜e (ass âŠ™ lidgg r (G.âˆ˜ax Î¹1.iddom âŠ™ G.id)) r ] /
                       Î¹2.fnc {G.â‚’ Y} â„‚.âˆ˜ Î¹2.fncâ»Â¹ {G.â‚’ Y}                  ~[ Î¹2.idcod ]âˆ
                       â„‚.idar (G.â‚’ Y) âˆ
              where open ecategory-aux-only â„‚


eqv-tr : {ğ”¸ ğ”¹ â„‚ : ecategory}{F : efunctor ğ”¸ ğ”¹}
         {G : efunctor ğ”¹ â„‚}{invG : efunctor â„‚ ğ”¹}{H : efunctor ğ”¸ â„‚}
            â†’ is-equivalence-pair G invG â†’ G â—‹ F â‰…â‚ H â†’ invG â—‹ H â‰…â‚ F
eqv-tr {F = F} {G} {invG} {H} eqvG tr =
  natiso-vcmp â—‹lid
              (natiso-vcmp (natiso-hcmp Î¹2 â‰…â‚refl)
                           (natiso-vcmp (â—‹ass {F = F} {G} {invG})
                                        (natiso-hcmp (â‰…â‚refl {F = invG}) (â‰…â‚sym tr))))
               where open is-equivalence-pair eqvG


record is-equivalence {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») : Setâ‚ where
  field
    invF : efunctor ğ”» â„‚
    iseqvp : is-equivalence-pair F invF
  open is-equivalence-pair iseqvp public

record is-adj-equivalence {â„‚ ğ”» : ecategory}(F : efunctor â„‚ ğ”») : Setâ‚ where
  {-field
    eqv : is-equivalence F
  open is-equivalence eqv public-}
  field
    invF : efunctor ğ”» â„‚
    isadj : is-adj-equivalence-pair F invF --iseqvp
  open is-adj-equivalence-pair isadj public


-- Other properties of funtors

record is-full {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor F
  field
    full-ar : {X Y : â„‚.Obj} â†’ || ğ”».Hom (F.â‚’ X) (F.â‚’ Y) || â†’ || â„‚.Hom X Y ||
    full-pf : {X Y : â„‚.Obj} {g : || ğ”».Hom (F.â‚’ X) (F.â‚’ Y) ||}
                    â†’ F.â‚ (full-ar g) ğ”».~ g
  full-pfË¢ : {X Y : â„‚.Obj} {g : || ğ”».Hom (F.â‚’ X) (F.â‚’ Y) ||}
                    â†’ g ğ”».~ F.â‚ (full-ar g)
  full-pfË¢ =  full-pf Ë¢
           where open ecategory-aux-only ğ”»
  full-pfg : {X Y : â„‚.Obj} {g g' : || ğ”».Hom (F.â‚’ X) (F.â‚’ Y) ||}
                    â†’ g ğ”».~ g' â†’ F.â‚ (full-ar g) ğ”».~ g'
  full-pfg pf = full-pf âŠ™ pf
              where open ecategory-aux-only ğ”»
  full-pfgË¢ : {X Y : â„‚.Obj} {g g' : || ğ”».Hom (F.â‚’ X) (F.â‚’ Y) ||}
                    â†’ g ğ”».~ g' â†’ g' ğ”».~ F.â‚ (full-ar g)
  full-pfgË¢ pf = full-pfg pf Ë¢
              where open ecategory-aux-only ğ”»


record is-faithful {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor F
  field
    faith-pf : {X Y : â„‚.Obj} {f g : || â„‚.Hom X Y ||}
                  â†’ F.â‚ f ğ”».~ F.â‚ g â†’ f â„‚.~ g


record is-ess-surjective-ob {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor F
  open iso-defs ğ”»
  field
    ob : ğ”».Obj â†’ â„‚.Obj
    ar : (Y : ğ”».Obj) â†’ || ğ”».Hom (F.â‚’ (ob Y)) Y ||
    iso : (Y : ğ”».Obj) â†’ is-iso (ar Y)



-- Essential equivalences

record is-ess-equivalence {â„‚ ğ”» : ecategory} (F : efunctor â„‚ ğ”») : Setâ‚ where
  private
    module â„‚ = ecategory â„‚
    module ğ”» = ecategory ğ”»
    module F = efunctor F
  field
    isfull : is-full F
    isfaithful : is-faithful F
    isesurjobj : is-ess-surjective-ob F
