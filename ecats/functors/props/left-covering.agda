
{-# OPTIONS --without-K #-}

module ecats.functors.props.left-covering where

open import ecats.basic-defs.ecat-def&not
open import ecats.basic-defs.all-arrows
open import ecats.basic-defs.eqv-rel
open import ecats.basic-defs.regular-ecat
open import ecats.basic-defs.exact-ecat
open import ecats.basic-props.all
open import ecats.finite-limits.all
open import ecats.functors.defs.efunctor-d&n
open import ecats.functors.defs.natural-transformation
open import ecats.functors.defs.basic-defs
open import ecats.functors.defs.preserving-functor
open import ecats.functors.defs.left-covering
open import ecats.functors.props.left-covering.left-covering-regular public
open import ecats.functors.props.left-covering.left-covering-basic public




-- Postcomposing a left covering functor with an exact functor yields a left covering functor

module exact+lcov-is-lcov {â„‚ ğ”» ğ”¼ : ecategory}(flğ”» : has-fin-limits ğ”»)
                          {F : efunctor â„‚ ğ”»}{G : efunctor ğ”» ğ”¼}
                          (lcovF : is-left-covering F)(exG : is-exact-functor G)
                          where
  private
    module â„‚ where
      open ecategory â„‚ public
      open comm-shapes â„‚ public
      open epis&monos-defs â„‚ public
      open epis&monos-props â„‚ public
      open finite-limits-d&p â„‚ public
      open finite-weak-limits-d&p â„‚ public
      open limitsâ†’weak-limits â„‚ public
      --open relations-among-limit-diagrams â„‚ public
    module ğ”» where
      open ecategory ğ”» public
      open comm-shapes ğ”» public
      open iso-defs ğ”» public
      open epis&monos-defs ğ”» public
      open epis&monos-props ğ”» public
      open finite-limits-d&p ğ”» public
      open finite-weak-limits-d&p ğ”» public
      open limitsâ†’weak-limits ğ”» public
      open relations-among-limit-diagrams ğ”» public
    module ğ”¼ where
      open ecategory ğ”¼ public
      open comm-shapes ğ”¼ public
      open iso-defs ğ”¼ public
      open epis&monos-defs ğ”¼ public
      open epis&monos-props ğ”¼ public
      open finite-limits-d&p ğ”¼ public
      open finite-weak-limits-d&p ğ”¼ public
      open limitsâ†’weak-limits ğ”¼ public
      open relations-among-limit-diagrams ğ”¼ public
    module flğ”» where
      open has-fin-limits flğ”» public
      open has-terminal hastrm public
      open has-bin-products hasprd using (prd-of) public
      open has-equalisers haseql using (eql-of) public
      open has-pullbacks haspb using (pb-of) public
      open has-bows hasbw using (bw-of) public
    module F where
      open efunctor-aux F public
      module lcov = is-left-covering lcovF
    module G where
      open efunctor-aux G public
      module ex = is-exact-functor exG
    module Gâ—‹F = efunctor-aux (G â—‹ F)

  module cmp-lcov-trm {X : â„‚.Obj}(wtrmX : â„‚.is-wterminal X){TE : ğ”¼.Obj}(trmTE : ğ”¼.is-terminal TE)
                      (cov! : || ğ”¼.Hom (Gâ—‹F.â‚’ X) TE ||)
                      where
    private
      module wT = â„‚.is-wterminal wtrmX
      TD : ğ”».Obj
      TD = flğ”».trmOb
      module TD = ğ”».is-terminal flğ”».istrm
      module TE = ğ”¼.is-terminal trmTE
      module GTD = ğ”¼.is-terminal (G.ex.pres-!-pf flğ”».istrm)
      covD-ar : || ğ”».Hom (F.â‚’ X) TD ||
      covD-ar = TD.! (F.â‚’ X)
      covD : ğ”».is-regular-epi covD-ar
      covD = F.lcov.trmuniv-is-repi wtrmX flğ”».istrm covD-ar
      GcovD : ğ”¼.is-regular-epi (G.â‚ covD-ar)
      GcovD = G.ex.pres-repi-pf covD
      TEâ‰…GTD : ğ”¼.is-iso (TE.! (G.â‚’ TD))
      TEâ‰…GTD = ğ”¼.mkis-iso (ğ”¼.!uq-isop (G.ex.pres-!-pf flğ”».istrm) trmTE)
      eq : TE.! (G.â‚’ TD) ğ”¼.âˆ˜ G.â‚ covD-ar ğ”¼.~ cov!
      eq = TE.!uqg
    cov!-repi : ğ”¼.is-regular-epi cov!
    cov!-repi = ğ”¼.iso-to-repi-is-repi-cod TEâ‰…GTD eq GcovD
  -- end cmp-lcov-trm

  module cmp-lcov-prd {X Y : â„‚.Obj}(wprdC : â„‚.wproduct-of X Y)
                      (prdE : ğ”¼.product-of (Gâ—‹F.â‚’ X) (Gâ—‹F.â‚’ Y))
                      {covprd : || ğ”¼.Hom (Gâ—‹F.â‚’ (â„‚.wproduct-of.O12 wprdC))
                                          (ğ”¼.product-of.O12 prdE) ||}
                      (trâ‚ : ğ”¼.product-of.Ï€â‚ prdE ğ”¼.âˆ˜ covprd ğ”¼.~ Gâ—‹F.â‚ (â„‚.wproduct-of.wÏ€â‚ wprdC))
                      (trâ‚‚ : ğ”¼.product-of.Ï€â‚‚ prdE ğ”¼.âˆ˜ covprd ğ”¼.~ Gâ—‹F.â‚ (â„‚.wproduct-of.wÏ€â‚‚ wprdC))
                      where
    private
      module wprdC = â„‚.wproduct-of wprdC
      prdD : ğ”».product-of (F.â‚’ X) (F.â‚’ Y)
      prdD = flğ”».prd-of (F.â‚’ X) (F.â‚’ Y)
      module prdD = ğ”».product-of prdD
      module prdE = ğ”¼.product-of prdE
      GprdD : ğ”¼.product-of (Gâ—‹F.â‚’ X) (Gâ—‹F.â‚’ Y)
      GprdD = ğ”¼.mkÃ—of (G.ex.pres-Ã—-pf prdD.Ã—isprd)
      module GprdD = ğ”¼.product-of GprdD
      covD-ar : || ğ”».Hom (F.â‚’ wprdC.O12) prdD.O12 ||
      covD-ar = prdD.< F.â‚ wprdC.wÏ€â‚ , F.â‚ wprdC.wÏ€â‚‚ >
      covD : ğ”».is-regular-epi covD-ar
      covD = F.lcov.prduniv-is-repi wprdC prdD prdD.Ã—trâ‚ prdD.Ã—trâ‚‚
      GcovD : ğ”¼.is-regular-epi (G.â‚ covD-ar)
      GcovD = G.ex.pres-repi-pf covD
      med : || ğ”¼.Hom (G.â‚’ prdD.O12) prdE.O12 ||
      med = prdE.< G.â‚ prdD.Ï€â‚ , G.â‚ prdD.Ï€â‚‚ >
      Eâ‰…GD : ğ”¼.is-iso med
      Eâ‰…GD = ğ”¼.mkis-iso Ã—uq-iso-pair
           where open ğ”¼.product-is-unique-uptoiso GprdD prdE
      isotr : med ğ”¼.âˆ˜ G.â‚ covD-ar ğ”¼.~ covprd
      isotr = prdE.Ã—uq
        (~proof prdE.Ï€â‚ ğ”¼.âˆ˜ med ğ”¼.âˆ˜ G.â‚ covD-ar   ~[ ass âŠ™ âˆ˜e r prdE.Ã—trâ‚ ] /
                G.â‚ prdD.Ï€â‚ ğ”¼.âˆ˜ G.â‚ covD-ar        ~[ G.âˆ˜ax prdD.Ã—trâ‚ ] /
                Gâ—‹F.â‚ wprdC.wÏ€â‚                    ~[ trâ‚ Ë¢ ]âˆ
                prdE.Ï€â‚ ğ”¼.âˆ˜ covprd âˆ)
        (~proof prdE.Ï€â‚‚ ğ”¼.âˆ˜ med ğ”¼.âˆ˜ G.â‚ covD-ar   ~[ ass âŠ™ âˆ˜e r prdE.Ã—trâ‚‚ ] /
                G.â‚ prdD.Ï€â‚‚ ğ”¼.âˆ˜ G.â‚ covD-ar        ~[ G.âˆ˜ax prdD.Ã—trâ‚‚ ] /
                Gâ—‹F.â‚ wprdC.wÏ€â‚‚                    ~[ trâ‚‚ Ë¢ ]âˆ
                prdE.Ï€â‚‚ ğ”¼.âˆ˜ covprd âˆ)
        where open ecategory-aux-only ğ”¼
    covprd-repi : ğ”¼.is-regular-epi covprd
    covprd-repi = ğ”¼.iso-to-repi-is-repi-cod Eâ‰…GD isotr GcovD    
  -- end cmp-lcov-prd

  module cmp-lcov-eql {X Y : â„‚.Obj}{f f' : || â„‚.Hom X Y ||}(weqlC : â„‚.wequaliser-of f f')
                      (eqlE : ğ”¼.equaliser-of (Gâ—‹F.â‚ f) (Gâ—‹F.â‚ f'))
                      {coveql : || ğ”¼.Hom (Gâ—‹F.â‚’ (â„‚.wequaliser-of.wEql weqlC))
                                          (ğ”¼.equaliser-of.Eql eqlE) ||}
                      (tr : ğ”¼.equaliser-of.eqlar eqlE ğ”¼.âˆ˜ coveql
                                     ğ”¼.~ Gâ—‹F.â‚ (â„‚.wequaliser-of.weqlar weqlC))
                      where
    private
      module weqlC = â„‚.wequaliser-of weqlC
      eqlD : ğ”».equaliser-of (F.â‚ f) (F.â‚ f')
      eqlD = flğ”».eql-of (F.â‚ f) (F.â‚ f')
      module eqlD = ğ”».equaliser-of eqlD
      module eqlE = ğ”¼.equaliser-of eqlE
      GeqlD : ğ”¼.equaliser-of (Gâ—‹F.â‚ f) (Gâ—‹F.â‚ f')
      GeqlD = ğ”¼.mkeql-of (G.ex.pres-eql-pf eqlD.iseql)
      module GeqlD = ğ”¼.equaliser-of GeqlD
      covD-ar : || ğ”».Hom (F.â‚’ weqlC.wEql) eqlD.Eql ||
      covD-ar = F.â‚ weqlC.weqlar eqlD.|eql[ F.âˆ˜âˆ˜ weqlC.weqleq  ]
      covD : ğ”».is-regular-epi covD-ar
      covD = F.lcov.eqluniv-is-repi weqlC eqlD (eqlD.eqltr (F.âˆ˜âˆ˜ weqlC.weqleq) )
      GcovD : ğ”¼.is-regular-epi (G.â‚ covD-ar)
      GcovD = G.ex.pres-repi-pf covD
      med : || ğ”¼.Hom (G.â‚’ eqlD.Eql) eqlE.Eql ||
      med = G.â‚ eqlD.eqlar eqlE.|eql[ G.âˆ˜âˆ˜ eqlD.eqleq ]
      Eâ‰…GD : ğ”¼.is-iso med
      Eâ‰…GD = ğ”¼.eqls-unv-is-iso eqlE.iseql GeqlD.iseql {med} (eqlE.eqltr (G.âˆ˜âˆ˜ eqlD.eqleq))
      isotr : med ğ”¼.âˆ˜ G.â‚ covD-ar ğ”¼.~ coveql
      isotr = eqlE.eqluq (~proof
        eqlE.eqlar ğ”¼.âˆ˜ med ğ”¼.âˆ˜ G.â‚ covD-ar    ~[ ass âŠ™ âˆ˜e r (eqlE.eqltr (G.âˆ˜âˆ˜ eqlD.eqleq)) ] /
        G.â‚ eqlD.eqlar ğ”¼.âˆ˜ G.â‚ covD-ar         ~[ G.âˆ˜ax (eqlD.eqltr (F.âˆ˜âˆ˜ weqlC.weqleq)) ] /
        Gâ—‹F.â‚ weqlC.weqlar                      ~[ tr Ë¢ ]âˆ
        eqlE.eqlar ğ”¼.âˆ˜ coveql âˆ)
            where open ecategory-aux-only ğ”¼
    coveql-repi : ğ”¼.is-regular-epi coveql
    coveql-repi = ğ”¼.iso-to-repi-is-repi-cod Eâ‰…GD isotr GcovD    
  -- end cmp-lcov-eql

  module cmp-lcov-pb {X Y Z : â„‚.Obj}{f : || â„‚.Hom X Z ||}{g : || â„‚.Hom Y Z ||}
                     (wpbC : â„‚.wpullback-of f g)(pbE : ğ”¼.pullback-of (Gâ—‹F.â‚ f) (Gâ—‹F.â‚ g))
                     {covpb : || ğ”¼.Hom (Gâ—‹F.â‚’ (â„‚.wpullback-of.ul wpbC)) (ğ”¼.pullback-of.ul pbE) ||}
                     (trâ‚ : ğ”¼.pullback-of.Ï€/â‚ pbE ğ”¼.âˆ˜ covpb ğ”¼.~ Gâ—‹F.â‚ (â„‚.wpullback-of.wÏ€/â‚ wpbC))
                     (trâ‚‚ : ğ”¼.pullback-of.Ï€/â‚‚ pbE ğ”¼.âˆ˜ covpb ğ”¼.~ Gâ—‹F.â‚ (â„‚.wpullback-of.wÏ€/â‚‚ wpbC))
                      where
    private
      module wpbC = â„‚.wpullback-of-not wpbC
      pbD : ğ”».pullback-of (F.â‚ f) (F.â‚ g)
      pbD = flğ”».pb-of (F.â‚ f) (F.â‚ g)
      module pbD = ğ”».pullback-of-not pbD
      module pbE = ğ”¼.pullback-of-not pbE
      GpbD : ğ”¼.pullback-of (Gâ—‹F.â‚ f) (Gâ—‹F.â‚ g)
      GpbD = ğ”¼.pbof-is2sq (G.ex.pres-ispbof-pf (ğ”».pbof-sq2is pbD))
      module GpbD = ğ”¼.pullback-of-not GpbD
      covD-ar : || ğ”».Hom (F.â‚’ wpbC.ul) pbD.ul ||
      covD-ar = pbD.âŸ¨ F.â‚ wpbC.wÏ€/â‚ , F.â‚ wpbC.wÏ€/â‚‚ âŸ©[ F.âˆ˜âˆ˜ wpbC.wÃ—/sqpf ]
      covD : ğ”».is-regular-epi covD-ar
      covD = F.lcov.pbuniv-is-repi wpbC pbD (pbD.Ã—/trâ‚ (F.âˆ˜âˆ˜ wpbC.wÃ—/sqpf)) (pbD.Ã—/trâ‚‚ (F.âˆ˜âˆ˜ wpbC.wÃ—/sqpf))
      GcovD : ğ”¼.is-regular-epi (G.â‚ covD-ar)
      GcovD = G.ex.pres-repi-pf covD
      med : || ğ”¼.Hom (G.â‚’ pbD.ul) pbE.ul ||
      med = pbE.âŸ¨ G.â‚ pbD.Ï€/â‚ , G.â‚ pbD.Ï€/â‚‚ âŸ©[ G.âˆ˜âˆ˜ pbD.Ã—/sqpf ]
      Eâ‰…GD : ğ”¼.is-iso med
      Eâ‰…GD = ğ”¼.pbs-unvar-is-iso GpbD pbE (pbE.Ã—/trâ‚ (G.âˆ˜âˆ˜ pbD.Ã—/sqpf)) (pbE.Ã—/trâ‚‚ (G.âˆ˜âˆ˜ pbD.Ã—/sqpf))
      isotr : med ğ”¼.âˆ˜ G.â‚ covD-ar ğ”¼.~ covpb
      isotr = pbE.Ã—/uq
        (~proof pbE.Ï€/â‚ ğ”¼.âˆ˜ med ğ”¼.âˆ˜ G.â‚ covD-ar   ~[ ass âŠ™ âˆ˜e r (pbE.Ã—/trâ‚ (G.âˆ˜âˆ˜ pbD.Ã—/sqpf)) ] /
                G.â‚ pbD.Ï€/â‚ ğ”¼.âˆ˜ G.â‚ covD-ar        ~[ G.âˆ˜ax (pbD.Ã—/trâ‚ (F.âˆ˜âˆ˜ wpbC.wÃ—/sqpf)) ] /
                Gâ—‹F.â‚ wpbC.wÏ€/â‚                    ~[ trâ‚ Ë¢ ]âˆ
                pbE.Ï€/â‚ ğ”¼.âˆ˜ covpb âˆ)
        (~proof pbE.Ï€/â‚‚ ğ”¼.âˆ˜ med ğ”¼.âˆ˜ G.â‚ covD-ar   ~[ ass âŠ™ âˆ˜e r (pbE.Ã—/trâ‚‚ (G.âˆ˜âˆ˜ pbD.Ã—/sqpf)) ] /
                G.â‚ pbD.Ï€/â‚‚ ğ”¼.âˆ˜ G.â‚ covD-ar        ~[ G.âˆ˜ax (pbD.Ã—/trâ‚‚ (F.âˆ˜âˆ˜ wpbC.wÃ—/sqpf)) ] /
                Gâ—‹F.â‚ wpbC.wÏ€/â‚‚                    ~[ trâ‚‚ Ë¢ ]âˆ
                pbE.Ï€/â‚‚ ğ”¼.âˆ˜ covpb âˆ)
        where open ecategory-aux-only ğ”¼
    covpb-repi : ğ”¼.is-regular-epi covpb
    covpb-repi = ğ”¼.iso-to-repi-is-repi-cod Eâ‰…GD isotr GcovD    
  -- end cmp-lcov-pb

  private
    cmplceql : is-left-covering-eql (G â—‹ F)
    cmplceql = record { eqluniv-is-repi = coveql-repi }
             where open cmp-lcov-eql
    cmplcpb : is-left-covering-pb (G â—‹ F)
    cmplcpb = record { pbuniv-is-repi = covpb-repi }
            where open cmp-lcov-pb  
  cmp-lcov :  has-weak-equalisers â„‚ â†’ has-weak-pullbacks â„‚ â†’ is-regular ğ”¼
                                  â†’ is-left-covering (G â—‹ F)
  cmp-lcov Cweql Cwpb regğ”¼ = record
    { lc! = record { trmuniv-is-repi = cov!-repi }
    ; lcÃ— = record { prduniv-is-repi = covprd-repi }
    ; lceql = cmplceql
    ; lcÃ—/ = cmplcpb
    ; lcbw = lcov-eql+pbâ†’lcov-bw regğ”¼ Cweql Cwpb cmplceql cmplcpb
    }
    where open cmp-lcov-trm
          open cmp-lcov-prd
-- end exact+lcov-is-lcov

exâ—‹lcov-is-lcov : {â„‚ ğ”» ğ”¼ : ecategory}(weqlâ„‚ : has-weak-equalisers â„‚)(wpbâ„‚ : has-weak-pullbacks â„‚)
                  (flğ”» : has-fin-limits ğ”»)(regğ”¼ : is-regular ğ”¼){F : efunctor â„‚ ğ”»}{G : efunctor ğ”» ğ”¼}
                     â†’ is-left-covering F â†’ is-exact-functor G
                       â†’ is-left-covering (G â—‹ F)
exâ—‹lcov-is-lcov weqlâ„‚ wpbâ„‚ flğ”» regğ”¼ lcovF exG = cmp-lcov weqlâ„‚ wpbâ„‚ regğ”¼
                                                where open exact+lcov-is-lcov flğ”» lcovF exG
